import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Monoidal.Category
import Mathlib.CategoryTheory.Monoidal.Braided
import Mathlib.Order.Monotone.Basic

import Enrichment.Binoidal.Category
import Enrichment.Premonoidal.Category
import Enrichment.Effectful.Braided
import Enrichment.Ordered.Category

open CategoryTheory
open BinoidalCategory
open PremonoidalCategory

structure DiagramPort (C: Type u) where
  value: C
  states: â„•

abbrev DiagramPort.state' {C: Type u} [TensorMonoid C]: DiagramPort C := âŸ¨tensorUnit', 1âŸ©
abbrev DiagramPort.state (C: Type u) [TensorMonoid C]: DiagramPort C := âŸ¨tensorUnit', 1âŸ©

open DiagramPort

instance {C: Type u} [TensorProduct C]: TensorProduct (DiagramPort C) where
  tensorObj := Î»âŸ¨X, nâŸ© âŸ¨Y, mâŸ© => âŸ¨X âŠ— Y, n + mâŸ©     

theorem DiagramPort.zero_tensor_left {C} [TensorProduct C] {X Y: DiagramPort C}
  (H: (X âŠ— Y).states = 0)
  : X.states = 0
  := Nat.le_zero.mp (Nat.le_trans (Nat.le_add_right _ _) (Nat.le_zero.mpr H))

theorem DiagramPort.zero_tensor_right {C} [TensorProduct C] {X Y: DiagramPort C} 
  (H: (X âŠ— Y).states = 0)
  : Y.states = 0
  := Nat.le_zero.mp (Nat.le_trans (Nat.le_add_left _ _) (Nat.le_zero.mpr H))

instance {C: Type u} [TensorMonoid C]: TensorMonoid (DiagramPort C) where
  tensorUnit' := âŸ¨tensorUnit', 0âŸ© 

inductive Diagram {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  : DiagramPort C -> DiagramPort C -> Type (max u v)
| identity (X): Diagram X X
| comp {X Y Z}: Diagram X Y -> Diagram Y Z -> Diagram X Z
| whiskerLeft {Y Z} (X): Diagram Y Z -> Diagram (X âŠ— Y) (X âŠ— Z) 
| whiskerRight {Y Z}: Diagram Y Z -> (X: DiagramPort C) -> Diagram (Y âŠ— X) (Z âŠ— X) 
| associator (X Y Z): Diagram ((X âŠ— Y) âŠ— Z) (X âŠ— (Y âŠ— Z)) 
| associator_inv (X Y Z): Diagram (X âŠ— (Y âŠ— Z)) ((X âŠ— Y) âŠ— Z)
| leftUnitor (X): Diagram (tensorUnit' âŠ— X) X
| leftUnitor_inv (X): Diagram X (tensorUnit' âŠ— X)
| rightUnitor (X): Diagram (X âŠ— tensorUnit') X
| rightUnitor_inv (X): Diagram X (X âŠ— tensorUnit')
| braiding (X Y): Diagram (X âŠ— Y) (Y âŠ— X)
| split: Diagram state' (state' âŠ— state')
| join: Diagram (state'âŠ— state') state'
| pure {X Y: C}: (Value.box X âŸ¶ Value.box Y) -> Diagram âŸ¨X, 0âŸ© âŸ¨Y, 0âŸ©
| effectful {X Y: C}: (X âŸ¶ Y) -> Diagram âŸ¨tensorUnit' âŠ— X, 1âŸ© âŸ¨tensorUnit' âŠ— Y, 1âŸ©

def Diagram.is_pure {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  {X Y: DiagramPort C}: Diagram X Y -> Prop
  | identity X => True
  | comp f g => f.is_pure âˆ§ g.is_pure 
  | whiskerLeft X f => f.is_pure
  | whiskerRight f X => f.is_pure
  | effectful _ => False
  | _ => True

theorem Diagram.state_conservation
  {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  {X Y: DiagramPort C}
  : Diagram X Y -> (X.states = 0 â†” Y.states = 0)
  | identity _ => by rfl
  | comp f g => by rw [f.state_conservation, g.state_conservation]
  | whiskerLeft âŸ¨_, nâŸ© f => by
    cases X; cases Y; cases n <;> simp_arith [tensorObj, f.state_conservation]
  | whiskerRight f âŸ¨_, nâŸ© => by
    cases X; cases Y; cases n <;> simp_arith [tensorObj, f.state_conservation]
  | associator âŸ¨_, _âŸ© âŸ¨_, _âŸ© âŸ¨_, _âŸ© => by simp [tensorObj, Nat.add_assoc]
  | associator_inv âŸ¨_, _âŸ© âŸ¨_, _âŸ© âŸ¨_, _âŸ© => by simp [tensorObj, Nat.add_assoc]
  | leftUnitor âŸ¨_, _âŸ© 
  | leftUnitor_inv âŸ¨_, _âŸ©
  | rightUnitor âŸ¨_, _âŸ©
  | rightUnitor_inv âŸ¨_, _âŸ© => by simp [tensorObj]
  | braiding âŸ¨_, _âŸ© âŸ¨_, _âŸ© => by simp [tensorObj, Nat.add_comm]
  | split | join | pure _ | effectful _ => by simp

theorem Diagram.no_forgetting
  {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  {X Y: DiagramPort C}
  (D: Diagram X Y): X.states = 0 -> Y.states = 0
  := by simp [D.state_conservation]

theorem Diagram.zero_left_pure {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  {X Y: DiagramPort C} (D: Diagram X Y)
  (HX: X.states = 0)
  : D.is_pure
  := by induction D with
  | comp f g If Ig => exact âŸ¨(If HX), (Ig (f.no_forgetting HX))âŸ©
  | whiskerLeft X _ If => exact If (DiagramPort.zero_tensor_right HX)
  | whiskerRight _ X If => exact If (DiagramPort.zero_tensor_left HX)
  | effectful _ => cases HX
  | _ => constructor

def Diagram.semantics {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [ğ’: PremonoidalCategory C]
  [ğ’®: SymmetricPremonoidalCategory C]
  [â„°: EffectfulCategory C]
  {X Y: DiagramPort C}
  : Diagram X Y -> (X.value âŸ¶ Y.value)
| identity X => ğŸ™ X.value
| comp f g => f.semantics â‰« g.semantics
| whiskerLeft Z f => ğ’.whiskerLeft Z.value f.semantics
| whiskerRight f Z => ğ’.whiskerRight f.semantics Z.value
| associator X Y Z => (ğ’.associator X.value Y.value Z.value).hom
| associator_inv X Y Z => (ğ’.associator X.value Y.value Z.value).inv
| leftUnitor X => (ğ’.leftUnitor X.value).hom
| leftUnitor_inv X => (ğ’.leftUnitor X.value).inv
| rightUnitor X => (ğ’.rightUnitor X.value).hom
| rightUnitor_inv X => (ğ’.rightUnitor X.value).inv
| braiding X Y => (ğ’®.braiding X.value Y.value).hom
| split => (ğ’.leftUnitor _).inv
| join => (ğ’.leftUnitor _).hom
| pure f => â„°.inclusion.map' f
| effectful f => (ğ’.leftUnitor _).hom â‰« f â‰« (ğ’.leftUnitor _).inv

def Diagram.pure_semantics {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [ğ’±: PremonoidalCategory (Value C)]
  [ğ’®: SymmetricPremonoidalCategory (Value C)]
  {X Y: DiagramPort C}
  : (D: Diagram X Y) -> D.is_pure -> (Value.box X.value âŸ¶ Value.box Y.value)
| identity X, _ => ğŸ™ (Value.box X.value)
| comp f g, Hx => f.pure_semantics Hx.1 â‰« g.pure_semantics Hx.2
| whiskerLeft X f, Hx => ğ’±.whiskerLeft X.value (f.pure_semantics Hx)
| whiskerRight f X, Hx => ğ’±.whiskerRight (f.pure_semantics Hx) X.value
| associator X Y Z, _ => (ğ’±.associator X.value Y.value Z.value).hom
| associator_inv X Y Z, _ => (ğ’±.associator X.value Y.value Z.value).inv
| leftUnitor X, _ => (ğ’±.leftUnitor X.value).hom
| leftUnitor_inv X, _ => (ğ’±.leftUnitor X.value).inv
| rightUnitor X, _ => (ğ’±.rightUnitor X.value).hom
| rightUnitor_inv X, _ => (ğ’±.rightUnitor X.value).inv
| braiding X Y, _ => (ğ’®.braiding X.value Y.value).hom
| split, Hx => (ğ’±.leftUnitor tensorUnit').inv
| join, Hx => (ğ’±.leftUnitor tensorUnit').hom
| pure f, _ => f
| effectful _, Hx => Hx.elim

theorem Diagram.semantic_inclusion {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [SymmetricPremonoidalCategory C]
  [â„°: EffectfulCategory C]
  [ğ’®: SymmetricEffectfulCategory C]
  {X Y: DiagramPort C}
  : (D: Diagram X Y) -> (H: D.is_pure) -> â„°.inclusion.map' (D.pure_semantics H) = D.semantics
  | identity _, _ => â„°.inclusion.map_id
  | comp f g, Hx => by
    rw [
      pure_semantics,
      â„°.inclusion.map_comp',
      f.semantic_inclusion Hx.1,
      g.semantic_inclusion Hx.2
    ]
    rfl
  | whiskerLeft Z f, Hx => by
    rw [
      pure_semantics,
      â„°.inclusion_whiskerLeft,
      f.semantic_inclusion Hx
    ]
    rfl
  | whiskerRight f Z, Hx => by
    rw [
      pure_semantics,
      â„°.inclusion_whiskerRight,
      f.semantic_inclusion Hx
    ]
    rfl
  | associator X Y Z, _ => â„°.inclusion_associator X.value Y.value Z.value
  | associator_inv X Y Z, _ => â„°.inclusion_associator_inv X.value Y.value Z.value
  | leftUnitor X, _ => â„°.inclusion_leftUnitor X.value
  | leftUnitor_inv X, _ => â„°.inclusion_leftUnitor_inv X.value
  | rightUnitor X, _ => â„°.inclusion_rightUnitor X.value
  | rightUnitor_inv X, _ => â„°.inclusion_rightUnitor_inv X.value
  | braiding X Y, _ => ğ’®.inclusion_braiding X.value Y.value
  | pure _, _ => rfl
  | split, Hx => â„°.inclusion_leftUnitor_inv _
  | join, Hx => â„°.inclusion_leftUnitor _
  | effectful _, Hx => by cases Hx

inductive Diagram.inverses {C: Type u}
  [TensorMonoid C]
  [Quiver.{v} (Value C)]
  [Quiver.{v} C]
  : {X Y: DiagramPort C} -> Diagram X Y -> Diagram Y X -> Prop
| associator (X Y Z): inverses (associator X Y Z) (associator_inv X Y Z)
| leftUnitor (X): inverses (leftUnitor X) (leftUnitor_inv X)
| rightUnitor (X): inverses (rightUnitor X) (rightUnitor_inv X)
| braiding (X Y): inverses (braiding X Y) (braiding Y X)

theorem Diagram.inverses.semantics {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [ğ’®: SymmetricPremonoidalCategory C]
  [EffectfulCategory C]
  {X Y: DiagramPort C}
  (f: Diagram X Y) (g: Diagram Y X)
  (I: f.inverses g) 
  : f.semantics â‰« g.semantics = ğŸ™ X.value âˆ§ g.semantics â‰« f.semantics = ğŸ™ Y.value
  := by cases I <;> constructor <;> simp [Diagram.semantics, ğ’®.symmetry] <;> rfl

inductive Diagram.association {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [ğ’±: PremonoidalCategory (Value C)]
  [ğ’®: SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [â„°: EffectfulCategory C]
  (P: {X Y: DiagramPort C} -> Diagram X Y -> Prop)
  : {X Y: DiagramPort C} -> Diagram X Y -> Diagram X Y -> Prop
  | identity_left {X Y} (f: Diagram X Y): association P (comp f (identity Y)) f
  | identity_right {X Y} (f: Diagram X Y): association P (comp (identity X) f) f
  | comp_assoc {X Y Z W} {f: Diagram X Y} {g: Diagram Y Z} {h: Diagram Z W}
    : association P (comp f (comp g h)) (comp (comp f g) h)
  | inv_comp {X Y} {f: Diagram X Y} {g: Diagram Y X}
    : inverses f g -> association P (comp f g) (identity X)
  | whiskerLeft_identity (X Y)
    : association P (whiskerLeft X (identity Y)) (identity (X âŠ— Y))
  | whiskerRight_identity (X Y)
    : association P (whiskerRight (identity X) Y) (identity (X âŠ— Y))
  | sliding {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚} (f: Diagram Xâ‚ Yâ‚) (g: Diagram Xâ‚‚ Yâ‚‚)
    : P f âˆ¨ P g -> association P 
      (comp (whiskerRight f Xâ‚‚) (whiskerLeft Yâ‚ g)) 
      (comp (whiskerLeft Xâ‚ g) (whiskerRight f Yâ‚‚))
  | associator_left {X Y Z X'} (f: Diagram X X')
    : association P
      (comp (whiskerRight (whiskerRight f Y) Z) (associator X' Y Z))
      (comp (associator X Y Z) (whiskerRight f (Y âŠ— Z)))
  | associator_mid {X Y Z Y'} (f: Diagram Y Y')
    : association P
      (comp (whiskerRight (whiskerLeft X f) Z) (associator X Y' Z))
      (comp (associator X Y Z) (whiskerLeft X (whiskerRight f Z)))
  | associator_right {X Y Z Z'} (f: Diagram Z Z')
    : association P
      (comp (whiskerLeft (X âŠ— Y) f) (associator X Y Z'))
      (comp (associator X Y Z) (whiskerLeft X (whiskerLeft Y f)))
  | leftUnitor {X Y} (f: Diagram X Y)
    : association P
      (comp (leftUnitor X) f)
      (comp (whiskerLeft tensorUnit' f) (leftUnitor Y))
  | rightUnitor {X Y} (f: Diagram X Y)
    : association P
      (comp (rightUnitor X) f)
      (comp (whiskerRight f tensorUnit') (rightUnitor Y))
  | braiding_left {X Y Z} (f: Diagram X Y)
    : association P
      (comp (whiskerLeft Z f) (braiding Z Y))
      (comp (braiding Z X) (whiskerRight f Z))
  | braiding_right {X Y Z} (f: Diagram X Y)
    : association P
      (comp (whiskerRight f Z) (braiding Y Z))
      (comp (braiding X Z) (whiskerLeft Z f))
  | triangle (X Y)
    : association P
      (comp (associator X tensorUnit' Y) (whiskerLeft X (leftUnitor Y)))
      (whiskerRight (rightUnitor X) Y)
  | pentagon (X Y Z W)
    : association P
      (comp (associator (X âŠ— Y) Z W) (associator X Y (Z âŠ— W)))
      (comp (whiskerRight (associator X Y Z) W) 
        (comp (associator X (Y âŠ— Z) W) (whiskerLeft X (associator Y Z W))))
  | hexagon (X Y Z)
    : association P
      (comp (associator X Y Z) (comp (braiding X (Y âŠ— Z)) (associator Y Z X)))
      (comp (whiskerRight (braiding X Y) Z) (comp (associator Y X Z) (whiskerLeft Y (braiding X Z))))
  | hoop: association P (comp split join) (identity state')
  | pure_identity (X): association P (pure (ğŸ™ X)) (identity _)
  | pure_composes {X Y Z: C} (f: Value.box X âŸ¶ Value.box Y) (g: Value.box Y âŸ¶ Value.box Z)
    : association P (comp (pure f) (pure g)) (pure (f â‰« g))
  | pure_left {X Y Z: C} (f: Value.box X âŸ¶ Value.box Y)
    : association P (whiskerLeft âŸ¨Z, 0âŸ© (pure f)) (pure (ğ’±.whiskerLeft Z f))
  | pure_right {X Y Z: C} (f: Value.box X âŸ¶ Value.box Y)
    : association P (whiskerRight (pure f) âŸ¨Z, 0âŸ©) (pure (ğ’±.whiskerRight f Z))
  | pure_associator (X Y Z: C)
    : association P (@pure C _ _ _ _ _ (ğ’±.associator X Y Z).hom) (associator âŸ¨X, 0âŸ© âŸ¨Y, 0âŸ© âŸ¨Z, 0âŸ©)
  | pure_leftUnitor (X: C)
    : association P (@pure C _ _ _ _ _ (ğ’±.leftUnitor X).hom) (leftUnitor âŸ¨X, 0âŸ©)
  | pure_rightUnitor (X: C)
    : association P (@pure C _ _ _ _ _ (ğ’±.rightUnitor X).hom) (rightUnitor âŸ¨X, 0âŸ©)
  | pure_braiding (X Y: C)
    : association P (@pure C _ _ _ _ _ (ğ’®.braiding X Y).hom) (braiding âŸ¨X, 0âŸ© âŸ¨Y, 0âŸ©)
  | effectful_identity (X: C)
    : association P (effectful (ğŸ™ X)) (identity âŸ¨tensorUnit' âŠ— X, 1âŸ©)
  --TODO: effectful whiskering?
  | effectful_inclusion_left {X Y Z: C} (f: X âŸ¶ Y) (g: Value.box Y âŸ¶ Value.box Z)
    : association P 
      (effectful (f â‰« â„°.inclusion.map' g))
      (comp (effectful f) (whiskerLeft state' (pure g)))
  | effectful_inclusion_right {X Y Z: C} (f: Value.box X âŸ¶ Value.box Y) (g: Y âŸ¶ Z)
    : association P 
      (effectful (â„°.inclusion.map' f â‰« g))
      (comp (whiskerLeft state' (pure f)) (effectful g))

def Diagram.isotopy {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [EffectfulCategory C]
  : {X Y: DiagramPort C} -> Diagram X Y -> Diagram X Y -> Prop
  := Diagram.association (Î»_ => True)

inductive Diagram.isotopic {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [EffectfulCategory C]
  : {X Y: DiagramPort C} -> Diagram X Y -> Diagram X Y -> Prop
| isotopy {D E: Diagram X Y}: D.isotopy E -> D.isotopic E
| isotopy_inv {D E: Diagram X Y}: E.isotopy D -> D.isotopic E
| congr_comp {D D': Diagram X Y} {E E': Diagram Y Z}
  : D.isotopic D' -> E.isotopic E' -> (comp D E).isotopic (comp D' E')
| congr_whiskerLeft {D D': Diagram X Y} (Z)
  : D.isotopic D' -> (whiskerLeft Z D).isotopic (whiskerLeft Z D')
| congr_whiskerRight {D D': Diagram X Y}
  : D.isotopic D' -> (Z: DiagramPort C) -> (whiskerRight D Z).isotopic (whiskerRight D' Z)
| refl (D: Diagram X Y): D.isotopic D
| trans (D E F: Diagram X Y): D.isotopic E -> E.isotopic F -> D.isotopic F

inductive Diagram.homotopic {C: Type u}
  [TensorMonoid C]
  [Category (Value C)]
  [Category C]
  [PremonoidalCategory (Value C)]
  [SymmetricPremonoidalCategory (Value C)]
  [MonoidalCategory' (Value C)]
  [PremonoidalCategory C]
  [EffectfulCategory C]
  [ğ’±: OrderedCategory (Value C)]
  [ğ’: OrderedCategory C]
  : {X Y: DiagramPort C} -> Diagram X Y -> Diagram X Y -> Prop
| isotopy {D E: Diagram X Y}: D.isotopy E -> D.homotopic E
| isotopy_inv {D E: Diagram X Y}: E.isotopy D -> D.homotopic E
| congr_pure {X Y: C} (f g: Value.box X âŸ¶ Value.box Y)
  : ğ’±.hom_ord.le f g -> (pure f).homotopic (pure g)
| congr_effectful {X Y: C} (f g: X âŸ¶ Y)
  : ğ’.hom_ord.le f g -> (effectful f).homotopic (effectful g)
| congr_comp {D D': Diagram X Y} {E E': Diagram Y Z}
  : D.homotopic D' -> E.homotopic E' -> (comp D E).homotopic (comp D' E')
| congr_whiskerLeft {D D': Diagram X Y} (Z)
  : D.homotopic D' -> (whiskerLeft Z D).homotopic (whiskerLeft Z D')
| congr_whiskerRight {D D': Diagram X Y}
  : D.homotopic D' -> (Z: DiagramPort C) -> (whiskerRight D Z).homotopic (whiskerRight D' Z)
| refl (D: Diagram X Y): D.homotopic D
| trans (D E F: Diagram X Y): D.homotopic E -> E.homotopic F -> D.homotopic F
